//@version=5
strategy("BB Strategy TS", overlay=true, initial_capital = 5000, default_qty_type = strategy.percent_of_equity, default_qty_value = 50)

// Use the last candle close as the base for our calculations
source = close

// Expontential moving average lookback buffer (candles)
length = input.int(20) 

// Lookback for RSI
RSIlength = input.int(14)

//RSI threshold for opening a position
RSIlimit = input.int(60) 

//BB standard deviation multiplier
mult = input.float(2.0, minval=0.001, maxval=50)

// Stop loss at 0.25% of the position size
stopPerc = input.float(0.98, minval=0.1, step=0.1) 

// Create Bollinger Band indicators
basis = ta.ema(source, length)
dev = mult * ta.stdev(source, length)
upper = basis + dev
lower = basis - dev
plot(upper, title="BB_upper", color=color.blue, linewidth=1, style=plot.style_line)
plot(lower, title="BB_lower", color=color.blue, linewidth=1, style=plot.style_line)
plot(basis, title="BB_basis", color=color.yellow, linewidth=1, style=plot.style_line)

// Calculate RElative Strength Indicator
RSI = ta.rsi(close, RSIlength)

// Inputs that configure the backtest's date range
useDateFilter = input.bool(true, title="Filter Date Range of Backtest", group="Backtest Time Period")
backtestStartDate = input.time(timestamp("1 Jan 2022"), title="Start Date", group="Backtest Time Period")
backtestEndDate = input.time(timestamp("1 Apr 2023"), title="End Date", group="Backtest Time Period")

// Check if current bar falls inside the date range
inTradeWindow = not useDateFilter or (time >= backtestStartDate and
     time < backtestEndDate)

// Enter to the position when the current close price crosses upper BB band
buyEntry = ta.crossover(source, upper)

// Exit the position if the close price is under the moving average
buyExitPrice = ta.crossunder(source, basis) 

// Set stop loss traget
buyStopPrice = strategy.position_avg_price * stopPerc
buyStop = low < buyStopPrice

// Open a new position if we have crossed the upper Bollinger Band 
// and our RSI is strong enough
if (buyEntry and inTradeWindow and RSI > RSIlimit and not (strategy.position_size > 0) and not (strategy.position_size < 0))
	strategy.entry("BBLE", strategy.long, oca_name="BB Entry", comment="Bollinger band entry")

// Close the position if we have dropped below the EMA
if (strategy.position_size > 0)
	strategy.close_all(buyExitPrice, comment="Moving average exit")

// Close the position if we have triggered a stop loss
if (strategy.position_size > 0)
	strategy.close_all(buyStop, comment="Stop loss exit")